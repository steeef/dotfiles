ZSHDIR="${HOME}/.zsh"
export ZSHDIR
ZGEN_COMPINIT_FLAGS='-i'
ZSH_COMPDUMP="${HOME}/.zcompdump_${ZSH_VERSION}"
ZGEN_CUSTOM_COMPDUMP="${ZSH_COMPDUMP}"
export ZSH_COMPDUMP

# disable oh-my-zsh update
export DISABLE_AUTO_UPDATE='true'

# correct spelling for commands
setopt correct
# turn off correct for filenames
unsetopt correctall

# history options
setopt hist_expire_dups_first
setopt hist_ignore_all_dups
setopt hist_ignore_dups
setopt hist_ignore_space
setopt hist_reduce_blanks
setopt hist_save_no_dups
setopt hist_verify
setopt inc_append_history
setopt extended_history
setopt share_history
unsetopt hist_beep

HISTSIZE=100000
SAVEHIST=100000
export HISTFILE="${HOME}/.zhistory" # The path to the history file.
export HISTORY_IGNORE="(ls|cd|cd ..|h|fc|pwd|exit|date|* --help)"

# Add some completions settings
setopt ALWAYS_TO_END     # Move cursor to the end of a completed word.
setopt AUTO_LIST         # Automatically list choices on ambiguous completion.
setopt AUTO_MENU         # Show completion menu on a successive tab press.
setopt AUTO_PARAM_SLASH  # If completed parameter is a directory, add a trailing slash.
setopt COMPLETE_IN_WORD  # Complete from both ends of a word.
unsetopt MENU_COMPLETE   # Do not autoselect the first completion entry.

# Miscellaneous settings
setopt INTERACTIVE_COMMENTS  # Enable comments in interactive shell.

setopt AUTO_CD # cd to directory if not a command

# Long running processes should return time after they complete. Specified
# in seconds.
REPORTTIME=60
TIMEFMT="%U user %S system %P cpu %*Es total"

# editor settings
EDITOR=nvim
VISUAL=nvim
export EDITOR VISUAL

# Expand aliases inline - see http://blog.patshead.com/2012/11/automatically-expaning-zsh-global-aliases---simplified.html
globalias() {
   if [[ $LBUFFER =~ ' [A-Z0-9]+$' ]]; then
     zle _expand_alias
     zle expand-word
   fi
   zle self-insert
}

zle -N globalias

bindkey " " globalias

bindkey -v # vi mode
autoload -z edit-command-line
zle -N edit-command-line
bindkey -M vicmd "^V" edit-command-line # open vim to edit command

bindkey "^R" history-incremental-search-backward
# PageUp/Down search history to complete command
bindkey "^[[I" history-beginning-search-backward
bindkey "^[[G" history-beginning-search-forward

# Speed up autocomplete, force prefix mapping
zstyle ':completion:*' accept-exact '*(N)'
zstyle ':completion:*' use-cache on
zstyle ':completion:*' cache-path ~/.zcache
zstyle -e ':completion:*:default' list-colors 'reply=("${PREFIX:+=(#bi)($PREFIX:t)*==34=34}:${(s.:.)LS_COLORS}")';

PATH="${HOME}/bin:${HOME}/.bin:/usr/local/bin:/usr/local/sbin:${PATH}"
export PATH

# Load and recompile before plugins
setopt nullglob
for file in ${ZSHDIR}/before/*.zsh; do
  source "${file}"
done
unsetopt nullglob

[ -f "${ZSHDIR}/zgenom_setup.zsh" ] && source "${ZSHDIR}/zgenom_setup.zsh"

# Load and recompile after plugins
setopt nullglob
for file in ${ZSHDIR}/after/*.zsh; do
  source "${file}"
done
unsetopt nullglob

# Make it easy to append your own customizations that override the above by
# loading all files from the ~/.zshrc.d directory
mkdir -p ~/.zshrc.d
if [ -n "$(/bin/ls ~/.zshrc.d)" ]; then
  for dotfile in ~/.zshrc.d/*
  do
    if [ -r "${dotfile}" ]; then
      source "${dotfile}"
    fi
  done
fi
PATH="${HOME}/.zgenom/bin:${PATH}"
export PATH

[[ ! -f ~/.p10k.zsh ]] || source ~/.p10k.zsh
